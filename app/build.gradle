apply plugin: 'com.android.application'
apply plugin: 'me.tatarka.retrolambda'

android {

    ext {
        APP_NAME = "Base Application"
    }

    compileSdkVersion 26
    buildToolsVersion "26.0.0"
    defaultConfig {
        applicationId "br.com.leonardomiyagi.baseapplication"
        minSdkVersion 16
        targetSdkVersion 26
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            resValue "string", "app_name", APP_NAME + " " + defaultConfig.versionName
        }
        staging {
            initWith(buildTypes.release)
            applicationIdSuffix ".staging"
            resValue "string", "app_name", APP_NAME + " " + defaultConfig.versionName + " [Staging]"
        }
        debug {
            applicationIdSuffix ".debug"
            resValue "string", "app_name", APP_NAME + " " + defaultConfig.versionName + " [Debug]"
        }
        applicationVariants.all { variant ->
            if (variant.buildType.name == android.buildTypes.release.name || variant.buildType.name == android.buildTypes.staging.name) {
                variant.outputs.each { output ->
                    def date = new Date();
                    def formattedDate = date.format('yyyy.MM.dd.HH:mm')
                    def filename = applicationId + "-" + formattedDate + "-v" + versionCode + "-n" + versionName + "-" + variant.name + ".apk"
                    output.outputFile = new File(output.outputFile.parent, filename)
                }
            }
        }
    }
    dataBinding {
        enabled true
    }
    compileOptions {
        targetCompatibility 1.8
        sourceCompatibility 1.8
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile "com.android.support:appcompat-v7:$support_version"
    testCompile 'junit:junit:4.12'

    // Rx
    compile 'io.reactivex.rxjava2:rxandroid:2.0.1'
    compile 'io.reactivex.rxjava2:rxjava:2.1.0'

    // Dagger2
    compile 'com.google.dagger:dagger-android:2.11'
    annotationProcessor 'com.google.dagger:dagger-compiler:2.11'
}
